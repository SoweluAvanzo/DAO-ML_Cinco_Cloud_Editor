id info.scce.cinco.product.flowgraph3
stylePath "model/FlowGraph.style"

@primeviewer
@postCreate("info.scce.cinco.product.flowgraph.hooks.InitializeFlowGraphModel")
graphModel FlowGraphDiagram3 {
	iconPath "icons/FlowGraph.png"
	diagramExtension "flowgraph"
	
	containableElements(Start, End, Activity, Swimlane, SubFlowGraph)
	
	attr EString as modelName
}

@icon("icons/Start.png")
@palette("Round Elements")
node Start {
	style greenCircle
	
	// allow exactly one outgoing Transition
	outgoingEdges (Transition[1,1])
}	

@icon("icons/End.png")
@palette("Round Elements")
node End{
	style redCircle
	
	/*
	
	allow an arbitrary number (>0) of incoming edges
	
	the following would have been valid as well, meaning the same:
	  incomingEdges (*[1,*])
	
	*/
	incomingEdges ({Transition,LabeledTransition}[1,*])
}

@icon("icons/Activity.png")
@palette("Rectangular Elements")
node Activity {
	// use the "blueTextRectangle" as this node's style and pass the attribute "name" as a parameter
	style blueTextRectangle("${name}")
	
	attr EString as name
	attr EString as description
	incomingEdges (*[1,*])
	outgoingEdges (LabeledTransition[1,*])
}

node SubFlowGraph {
	style greenTextRectangle("${subFlowGraph.modelName}")
	
	prime this::FlowGraphDiagram3 as subFlowGraph
	incomingEdges (*[1,*])
	outgoingEdges (LabeledTransition[1,*])
}

@icon("icons/Swimlane.png")
@palette("Rectangular Elements")
container Swimlane {
	style swimlane("${actor}")
	
	attr EString as actor
	containableElements (Start[1,1], Activity, End, SubFlowGraph)
}

edge Transition {
	style simpleArrow
}

edge LabeledTransition {
	style labeledArrow("${label}")
	
	attr EString as label
}
